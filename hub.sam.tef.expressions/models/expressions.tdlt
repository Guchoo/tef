syntax toplevel ModelTpl, ecorepath "platform:/plugin/hub.sam.tef.expressions/models/expressions.ecore" {	
    element FuncTpl for Function, idprop name {
        single for name, with IDENTIFIER;
        "(";
        sequence for parameters, with @ParamTpl, seperator ",", last false;
        ")";
        "=";
        single for body, with @ExprTpl;
    }
    element FuncIdTpl for Function {
        single for name, with IDENTIFIER;
    }
    element ModelTpl for Model {
        sequence for functions, with @FuncTpl, seperator ";", last true;
    }
    element ParamTpl for Parameter {
        single for name, with IDENTIFIER;
    }
    choice ExprTpl for Expression {		
		@Plus
		@TermTpl
    }
    choice TermTpl for Expression {
    	@Mul
    	@FactorTpl
    }
    choice FactorTpl for Expression {
    	@FuncCallTpl
    	@ParamAccessTpl
    	@NumberTpl    
    }
    element Mul for Mul {
        single for op1, with @TermTpl;
        "*";
        single for op2, with @FactorTpl;
    }
   	element Plus for Plus {
        single for op1, with @ExprTpl;
         "+";
         single for op2, with @TermTpl;
    }
    element ParamAccessTpl for ParameterAccess {
        single for parameter, with (ref for Parameter, with @ParamTpl);
    }
    element FuncCallTpl for FunctionCall {
        single for function, with (ref for Function, with @FuncIdTpl);
        "(";
        sequence for arguments, with @ExprTpl, seperator ",", last false;
        ")";
    }
    element NumberTpl for Number {
        single for value, with INTEGER;
    }
}